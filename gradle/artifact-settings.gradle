ext {
    mapLibreArtifactGroupId = 'org.maplibre.gl'
    mapLibreArtifactId = project.hasProperty('POM_ARTIFACT_ID') ? project.property('POM_ARTIFACT_ID') : System.getenv('POM_ARTIFACT_ID')
    mapLibreArtifactTitle = 'MapLibre Android Java Utilities'
    mapLibreArtifactDescription = project.hasProperty('POM_DESCRIPTION') ? project.property('POM_DESCRIPTION') : System.getenv('POM_DESCRIPTION')
    mapLibreDeveloperName = 'MapLibre'
    mapLibreDeveloperEmail = 'maplibre@maplibre.org'
    mapLibreDeveloperOrganization = 'MapLibre'
    mapLibreDeveloperOrganizationUrl = 'http://www.maplibre.org'
    mapLibreDeveloperId = 'maplibre'
    mapLibreArtifactUrl = 'https://github.com/maplibre/maplibre-java'
    mapLibreArtifactVcsUrl = 'https://github.com/maplibre/maplibre-java.git'
    mapLibreArtifactScmUrl = 'scm:git@github.com:maplibre/maplibre-java.git'
    mapLibreArtifactLicenseName = 'The Apache Software License, Version 2.0'
    mapLibreArtifactLicenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    versionName = getVersionName()
    isSnapshot = isSnapshot()
}

def getVersionName() {
    def version = System.getenv('VERSION')
    def deducedVersion = isSnapshot() ? getSnapshotVersion(version) : version
    project.logger.info("Project version detected: ${deducedVersion}")
    return deducedVersion
}

def isSnapshot() {
    def snapshotProp = findProperty("snapshot")
    return snapshotProp ? snapshotProp.asBoolean() : false
}

private static def getSnapshotVersion(String version) {
    def matcher = version =~ /^[v|V](\d+).(\d+)?.*/
    def major = (matcher[0][1] as Integer)
    def minor = (matcher[0][2] as Integer) + 1
    return "${major}.${minor}.0-SNAPSHOT"
}
